import java.text.SimpleDateFormat
import java.util.Date


plugins {
    id "java"
    // Apply the application plugin to add support for building a CLI application in Java.
	// This produces the distributions and scripts for any OS
	id "application"
	// For source code formatting
	id "com.diffplug.spotless" version "6.20.0"
}

/**
 * Project Properties
 */
group = 'ortus.boxlang'
java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

/**
 * Repositories for dependencies in order
 */
repositories {
    mavenLocal()
	mavenCentral()
}

/**
 * Project Dependencies
 */
dependencies {
    // Testing Dependencies
    testImplementation "org.junit.jupiter:junit-jupiter:5.+"
	testImplementation "org.mockito:mockito-core:5.+"
	testImplementation "com.google.truth:truth:1.+"

	// Runtime Dependencies

	// https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	implementation 'org.apache.commons:commons-lang3:3.13.0'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.jr/jackson-jr-all
	implementation 'com.fasterxml.jackson.jr:jackson-jr-all:2.15.2'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.jr/jackson-jr-annotation-support
	implementation 'com.fasterxml.jackson.jr:jackson-jr-annotation-support:2.13.3'
	// https://mvnrepository.com/artifact/com.github.ben-manes.caffeine/caffeine
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
	// https://mvnrepository.com/artifact/org.slf4j/slf4j-api
	implementation 'org.slf4j:slf4j-api:2.0.7'
	// https://mvnrepository.com/artifact/org.slf4j/slf4j-jdk14
	implementation 'org.slf4j:slf4j-jdk14:2.0.7'
}

/**
 * Project Wide Helper functions
 */
project.ext.bumpVersion = { boolean major = false, boolean minor = false, boolean patch = false ->

	def propertiesFile = file( '../gradle.properties' );
	def properties = new Properties();

	properties.load( propertiesFile.newDataInputStream() )
	def versionTarget = major ? 0 : minor ? 1 : 2

	def currentVersion = properties.getProperty( 'version' )
	def versionParts = currentVersion.split( '\\.' )
	def newPathVersion = versionParts[ versionTarget ].toInteger() + 1
	def newVersion = '';

	if( patch ){
		newVersion = "${versionParts[ 0 ]}.${versionParts[ 1 ]}.${newPathVersion}"
	} else if( minor ){
		newVersion = "${versionParts[ 0 ]}.${newPathVersion}.${versionParts[ 2 ]}"
	} else if( major ){
		newVersion = "${newPathVersion}.${versionParts[ 1 ]}.${versionParts[ 2 ]}"
	}

	properties.setProperty( 'version', newVersion )
	properties.store( propertiesFile.newWriter(), null )

	println "Bumped version from ${currentVersion} to ${newVersion}"
}

/**
 * Source Code Formatting
 */
spotless {
    java {
		target fileTree( "." ) {
            include "**/*.java"
            exclude "**/build/**", "bin/**"
        }
        eclipse().configFile( "../workbench/ortus-java-style.xml" )
    }
}


/**
 * Influence the Java compiler
 */
tasks.withType( JavaCompile ) {
    options.encoding = 'UTF-8'
	options.debug()
}

/**
 * Application Build
 */
application {
    // Define the main class for the application.
    mainClass = 'ortus.boxlang.runtime.BoxRunner'
}

/**
 * Control the Jar naming schema
 */
jar {
    archiveBaseName = 'ortus-boxlang-runtime'
    archiveVersion =  "${version}"
// 	manifest {
//       attributes 'Main-Class': 'com.example.main.Application'
//    }
}

/**
 * Copy runtime dependencies to build/runtime
 * Useful for testing and deebugging
 */
task getDependencies( type: Copy ) {
  from sourceSets.main.runtimeClasspath
  into 'build/dependencies/'
}

/**
 * Javadoc Task
 */
javadoc {
	//include 'ortus/boxlang/addition/**'
	//exclude 'ortus/boxlang/subtraction/**'
}

/**
 * Test Task Influence
 */
tasks.named( 'test' ) {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

/**
 * Custom tasks to run in the `build` task lifecycle
 */
 build.finalizedBy( javadoc )


/**
 * Bump the major version number
 */
task bumpMajorVersion {
	doLast{
		bumpVersion( true )
	}
}

/**
 * Bump the minor version number
 */
task bumpMinorVersion {
	doLast{
		bumpVersion( false, true )
	}
}

/**
 * Bump the patch version number
 */
task bumpPatchVersion {
	doLast{
		bumpVersion( false, false, true )
	}
}
