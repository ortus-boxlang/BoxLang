plugins {
    id 'java'
    id "com.diffplug.spotless" version "6.25.0"
	// https://github.com/harbby/gradle-serviceloader
    id "com.github.harbby.gradle.serviceloader" version "1.1.8"
	// https://github.com/johnrengelman/shadow
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.ortussolutions'
version = '1.0.0-SNAPSHOT'
java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
	// Until BoxLang is published to Maven Central
	// Look for it in the local build directory
	// You must run `./gradle build -x test` in the BoxLang project
    compileOnly files( '../../build/libs/ortus-boxlang-1.0.0-all.jar' )

    // Testing Dependencies
	testImplementation files( '../../build/libs/ortus-boxlang-1.0.0-all.jar' )
    testImplementation "org.junit.jupiter:junit-jupiter:5.+"
	testImplementation "org.mockito:mockito-core:5.+"
	testImplementation "com.google.truth:truth:1.+"
}

javadoc {
	//options.addBooleanOption( "Xdoclint:none", true )
	//exclude '**/boxlang/parser/**'
	options.addBooleanOption( 'html5', true )
}

/**
 * Docs are here:
 * - https://github.com/harbby/gradle-serviceloader,
 * - https://plugins.gradle.org/plugin/com.github.harbby.gradle.serviceloader
 * This generates the META-INF/services files for the ServiceLoader as part of the `build` task
 */
serviceLoader {
    serviceInterface 'ortus.boxlang.runtime.bifs.BIF'
    serviceInterface 'ortus.boxlang.runtime.components.Component'
    serviceInterface 'ortus.boxlang.runtime.async.tasks.IScheduler'
    serviceInterface 'ortus.boxlang.runtime.cache.providers.ICacheProvider'
	serviceInterface 'ortus.boxlang.runtime.events.IInterceptor'
}

shadowJar {
	manifest {
		attributes 'Description': 'BoxLangCompatModule'
	}
    destinationDirectory = file( "$rootDir/libs" )
}

clean {
    delete files( 'libs' )
}

test {
    useJUnitPlatform()
}
